# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Contributor: toha257 <toha257@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Tavian Barnes <tavianator@tavianator.com>
# Contributor: Yvan Janssens <yvanj@cyber.gent>
# Maintainer: Yvan Janssens <yvanj@cyber.gent>

_target="s390x-linux-gnu"
pkgname=${_target}-binutils
pkgver=2.39
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files (${_target})"
arch=(i686 x86_64 aarch64)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(glibc zlib)
checkdepends=(dejagnu bc)
options=(staticlibs !distcc !ccache)
source=(http://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig})
validpgpkeys=(3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F)
md5sums=('f7e986ae9ff06405cafb2e585ee36d27'
         'SKIP')

prepare() {
  mkdir -p binutils-build

  #cd binutils-gdb
  cd binutils-$pkgver

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
#  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd binutils-build

  "$srcdir/binutils-$pkgver/configure" \
      --prefix=/usr \
      --program-prefix=${_target}- \
      --with-lib-path=/usr/lib/binutils/${_target} \
      --with-local-prefix=/usr/lib/${_target} \
      --with-sysroot=/usr/${_target} \
      --enable-deterministic-archives \
      --enable-gold \
      --enable-ld=default \
      --enable-lto \
      --enable-plugins \
      --enable-relro \
      --enable-shared \
      --enable-threads \
      --disable-gdb \
      --disable-werror \
      --with-pic \
      --with-system-zlib \
      --disable-sim \
      --target=${_target} \
      --host=${CHOST} \
      --build=${CHOST}

  make configure-host
  make tooldir=/usr
}

check() {
  cd binutils-build

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # ignore failures in gold testsuite...
  make -k LDFLAGS="" check || true
}

package() {
  cd binutils-build
  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove unwanted files
  rm -rf "$pkgdir/usr/share"
  rm -f "$pkgdir/usr/bin/"{ar,as,ld,nm,objdump,ranlib,readelf,strip,objcopy}
  rm -f "$pkgdir"/usr/lib/ldscripts/elf32*
  rm -f "$pkgdir"/usr/lib/ldscripts/elf64p*
  rm -f "$pkgdir"/usr/include/ctf-api.h
  rm -f "$pkgdir"/usr/include/ctf.h
  rm -f "$pkgdir"/usr/lib/bfd-plugins/libdep.so
  rm -f "$pkgdir"/usr/lib/libctf-nobfd.a
  rm -f "$pkgdir"/usr/lib/libctf-nobfd.so
  rm -f "$pkgdir"/usr/lib/libctf-nobfd.so.0
  rm -f "$pkgdir"/usr/lib/libctf-nobfd.so.0.0.0
  rm -f "$pkgdir"/usr/lib/libctf.a
  rm -f "$pkgdir"/usr/lib/libctf.so
  rm -f "$pkgdir"/usr/lib/libctf.so.0
  rm -f "$pkgdir"/usr/lib/libctf.so.0.0.0
}
